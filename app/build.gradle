apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'koin'

android {
    compileSdkVersion build_config.compileSdkVersion
    buildToolsVersion build_config.buildToolsVersion

    defaultConfig {
        applicationId application_id

        minSdkVersion build_config.minSdkVersion
        targetSdkVersion build_config.targetSdkVersion

        versionCode build_config.version.code
        versionName build_config.version.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resConfigs 'en'

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
    }

    signingConfigs {
        debug {
            storeFile file('../release/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'

        }
        release {
            /*storeFile file('../release/jks-filename.jks')
            storePassword ''
            keyAlias ''
            keyPassword ''*/
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    sourceSets {
        development {
            java {
                srcDirs = ['src/main/java']
            }
        }
        production {
            java {
                srcDirs = ['src/main/java']
            }
        }
        main {
            res.srcDirs = [
                    file('src/main/res/layouts').listFiles(),
                    file('src/main/res/layouts/dialogs').listFiles(),
                    'src/main/res',
            ]
        }
    }

    productFlavors {
        def IP
        def VERSION
        def NEWS_API_KEY
        development {
            IP = "https://newsapi.org"
            VERSION = "v2"
            NEWS_API_KEY = "PASTE YOUR API KEY HERE"
            buildConfigField "String", 'BASE_URL', "\"$IP/\""
            buildConfigField "String", 'BASE_API_URL', "\"$IP/$VERSION/\""
            buildConfigField "String", 'NEWS_API_KEY', "\"Bearer $NEWS_API_KEY\""

            dimension "default"
            applicationIdSuffix ".development"
            resValue "string", "app_name", "$application_name - Development"
        }
        production {
            IP = "https://newsapi.org"
            VERSION = "v2"
            NEWS_API_KEY = "PASTE YOUR API KEY HERE"
            buildConfigField "String", 'BASE_URL', "\"$IP/\""
            buildConfigField "String", 'BASE_API_URL', "\"$IP/$VERSION/\""
            buildConfigField "String", 'NEWS_API_KEY', "\"Bearer $NEWS_API_KEY\""

            dimension "default"
            resValue "string", "app_name", application_name
        }
    }

    android.buildFeatures.dataBinding = true

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin Standard Library
    implementation libs.kotlin.stdLib.jdk

    // Custom Libraries
    implementation project(":smileyprogressview")

    // MultiDex Library
    implementation libs.multidex

    // Lifecycle Architecture Libraries
    implementation libs.android.architecture.lifecycle.extensions
    kapt libs.android.architecture.lifecycle.compiler
    kapt libs.android.architecture.lifecycle.livedata
    api libs.android.architecture.lifecycle.viewModel

    // RxJava Architecture Libraries
    implementation libs.android.architecture.rx.rxJava
    implementation libs.android.architecture.rx.rxAndroid
    implementation libs.android.architecture.rx.rxKotlin

    // Room Architecture Libraries
    implementation libs.android.architecture.room.runtime
    kapt libs.android.architecture.room.processor

    // Android Support Libraries
    implementation libs.android.support.appCompat
    implementation libs.android.support.ktx
    implementation libs.android.support.annotation
    implementation libs.android.support.core
    implementation libs.android.support.recyclerView
    implementation libs.android.support.cardView
    implementation libs.android.support.material

    // Permission Dispatcher Libraries
    implementation libs.permissions.runtime
    kapt libs.permissions.processor

    // Timber Library
    implementation libs.timber

    // Independent Pixel Libraries
    implementation libs.independentPixel.ssp
    implementation libs.independentPixel.sdp

    // Anko Library
    implementation libs.anko

    // Gson Json Converter Library
    implementation libs.gson

    // Koin Libraries
    implementation libs.koin.core
    implementation libs.koin.coreExt
    implementation libs.koin.scope
    implementation libs.koin.viewModel
    implementation libs.koin.fragment
    implementation libs.koin.androidXExt

    // Retrofit Api Calling Libraries
    implementation libs.retrofit.client
    implementation libs.retrofit.adapterRxJava2
    implementation libs.retrofit.converterGson
    implementation libs.retrofit.converterScalars

    // OkHttp Retrofit Dependency Libraries
    implementation libs.okhttp.client
    implementation libs.okhttp.logging

    // Glide Libraries
    implementation libs.glide.runtime
    kapt libs.glide.processor

    // Test Implementation Libraries
    testImplementation libs.junit
    testImplementation libs.junitExt
    androidTestImplementation libs.runner
    androidTestImplementation libs.espresso

}